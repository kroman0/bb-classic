*** Settings ***

Library  Selenium2Library  timeout=15  implicit_wait=0.1

Resource  keywords.txt

Suite Setup  Start app

Suite Teardown  Stop app

Test Setup  Start Browser For Test

Test Teardown  Close All Browsers and Report Status


*** Test Cases ***

Projects page should contains links to projects
    [Documentation]  Projects page should contains correct links to projects if company and projects state selected.
    [Tags]  projects  boostrap
    Given an projects page
    When click on "Company name #0" in active state
    Then link to Name of #0 should be visible
    And link to Name of #5 should not be visible
    And link to Name of #10 should not be visible
    And link to Name of #15 should be visible
    And link to Name of #20 should not be visible
    And link to Name of #25 should not be visible
    When click on "Company name #0" in on_hold state
    Then link to Name of #0 should not be visible
    And link to Name of #5 should not be visible
    And link to Name of #10 should be visible
    And link to Name of #15 should not be visible
    And link to Name of #20 should not be visible
    And link to Name of #25 should be visible
    When click on "Company name #0" in archived state
    Then link to Name of #0 should not be visible
    And link to Name of #5 should be visible
    And link to Name of #10 should not be visible
    And link to Name of #15 should not be visible
    And link to Name of #20 should be visible
    And link to Name of #25 should not be visible

Project navigation should work correctly
    [Documentation]  Projects navigation should change location and heading correctly.
    [Tags]  navigation  projects
    Given an projects page
    When click link  Name of #0
    Then location should be  ${APP_URL}/#projects/0
    And heading should be "Name of #0"
    And title should be  Name of #0 - Projects - Company name #0 - Companies - BB
    When click on "Messages" in projectnav
    Then location should be  ${APP_URL}/#projects/0/posts
    And heading should be "Posts"
    And title should be  Posts - Name of #0 - Projects - Company name #0 - Companies - BB
    When click on "To-Dos" in projectnav
    Then location should be  ${APP_URL}/#projects/0/todo_lists
    And heading should be "To-dos"
    And title should be  To-dos - Name of #0 - Projects - Company name #0 - Companies - BB
    When click on "Calendar" in projectnav
    Then location should be  ${APP_URL}/#projects/0/calendar
    And heading should be "Calendar"
    And title should be  Calendar - Name of #0 - Projects - Company name #0 - Companies - BB
    When click on "Time" in projectnav
    Then location should be  ${APP_URL}/#projects/0/time_entries
    And heading should be "Time"
    And title should be  Time - Name of #0 - Projects - Company name #0 - Companies - BB
    When click on "Files" in projectnav
    Then location should be  ${APP_URL}/#projects/0/files
    And heading should be "Files"
    And title should be  Files - Name of #0 - Projects - Company name #0 - Companies - BB
    When click on "Categories" in projectnav
    Then location should be  ${APP_URL}/#projects/0/categories
    And heading should be "Categories"
    And title should be  Categories - Name of #0 - Projects - Company name #0 - Companies - BB
    When click on "People" in projectnav
    Then location should be  ${APP_URL}/#projects/0/people
    And heading should be "People"
    And title should be  People - Name of #0 - Projects - Company name #0 - Companies - BB


*** Keywords ***

an projects page
    [Documentation]  Go to projects page.
    Given Test user login
    When click link  Projects
    Then wait for data loading

click on ${text} in ${state} state
    [Documentation]  Click on link in state block.
    Click on ${text} in ${state} state and wait effect

link to ${name} should be visible
    [Documentation]  Check if link is visible.
    Element Should Be Visible  link=${name}

link to ${name} should not be visible
    [Documentation]  Check if link is not visible.
    Element Should Not Be Visible  link=${name}
